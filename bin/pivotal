#! /usr/bin/env racket
#lang racket

(require json
         net/url
         racket/list
         racket/format
         racket/port)

;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;;         api
;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(struct story
  (name owned-by-id kind current-state owner-name) #:transparent)

(define PIVOTAL-URI
  "https://www.pivotaltracker.com/services/v5/projects")

(define ITERATION-PATH
  "/iterations?scope=current")

(define MEMBERSHIPS-PATH
  "/memberships")

(define (pivotal-url project-id)
  (define url-string
    (string-append PIVOTAL-URI "/" project-id ITERATION-PATH))
  (string->url  url-string))

(define (members-url project-id)
  (define url-string
    (string-append PIVOTAL-URI "/" project-id MEMBERSHIPS-PATH))
  (string->url  url-string))

(define (pivotal-header token)
  (list (string-append "X-TrackerToken:" token)))

(define (fetch url header)
  (define in (get-pure-port url header))
  (define out (open-output-string))
  (copy-port in out)
  (get-output-string out))

(define (api-call url header)
  (fetch url header))

(define (iteration-stories iteration)
  (hash-ref (car (string->jsexpr iteration)) 'stories))

(define (story-node->story story-node users)
    (story
     (hash-ref story-node 'name)
     (hash-ref story-node 'owned_by_id)
     (hash-ref story-node 'kind)
     (hash-ref story-node 'current_state)
     (hash-ref users (hash-ref story-node 'owned_by_id))))

(define (extract-stories iteration memberships)
  (let ([users (extract-users (string->jsexpr  memberships))])
    (map (lambda [s]
           (story-node->story s users))
         (iteration-stories iteration))))

(define (extract-users nodes)
  (let ([m (make-hash)])
    (for ([n nodes])
      (let* ([person (hash-ref n 'person)]
             [id (hash-ref person 'id)]
             [name (hash-ref person 'name)])
        (hash-set! m id name)))
    m))

(define (grouped stories)
  (group-by (lambda (s)
              (story-owner-name s))
            stories))

;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;;         files
;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(define (api->file url header filename)
  (call-with-output-file filename
    (lambda (out)
      (write (api-call url header) out))
    #:mode 'text
    #:exists 'replace))

;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;;         printing
;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(define (print-grouped grouped)
  (let ([formatter
         (lambda (story)
           (~a (story-current-state story)
               " - " (story-owner-name story)
               " - " (story-name story)
               #:max-width 90 #:limit-marker "..."))])
    (for ([g grouped])
      (for ([s g])
        (printf "~a\n" (formatter s))))))

;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;;         command-line
;;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(define (runner args)
  (let* ([token
          (vector-ref args 0)]
         [project-id
          (vector-ref args 1)]
         [header
          (pivotal-header token)]
         [iteration
          (api-call (pivotal-url project-id) header)]
         [users
          (api-call (members-url project-id) header)]
         [stories
          (grouped (extract-stories iteration users))])
    (print-grouped stories)))

(define (usage)
  (printf "Arguments needed: <token> <project-id>\n"))

(define (main)
  (define cli-args (current-command-line-arguments))
  (if (> 2 (vector-length cli-args))
      (usage)
      (runner cli-args)))

(main)
